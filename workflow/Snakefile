configfile: "config.yml"


container: "docker://ctbushman/shotguntree:latest"


MAIN_TARGET = (
    "RAxML_outgroupRootedTree.final"
    if config["OUTGROUP"]
    else "RAxML_midpointRootedTree.final"
)


# BENCHMARK_TARGET = "gene_tree_benchmark_report.txt"


rule all:
    input:
        MAIN_TARGET, #BENCHMARK_TARGET,


rule all_gene_files:
    input:
        expand("sequences/{gene}.fasta", gene=config["GENES"]),


rule all_filtered_files:
    input:
        expand("filtered-sequences/{gene}.fasta", gene=config["GENES"]),


rule align_fasta:
    input:
        "merged-sequences/{gene}.fasta",
    output:
        temp("aligned-sequences/{gene}.fasta"),
    log:
        "logs/align_fasta/{gene}.log",
    benchmark:
        "benchmarks/align_fasta/{gene}.txt"
    conda:
        "envs/muscle_env.yaml"
    shell:
        "muscle -in merged-sequences/{wildcards.gene}.fasta -out aligned-sequences/{wildcards.gene}.fasta"


rule filter_columns:
    # Identity op for now, okfasta
    input:
        "aligned-sequences/{gene}.fasta",
    output:
        "filtered-sequences/{gene}.fasta",
    log:
        "logs/filter_columns/{gene}.log",
    benchmark:
        "benchmarks/filter_columns/{gene}.txt"
    shell:
        "cp aligned-sequences/{wildcards.gene}.fasta filtered-sequences/{wildcards.gene}.fasta"


rule create_trees:
    input:
        "filtered-sequences/{gene}.fasta",
    output:
        temp("trees/RAxML_bestTree.{gene}"),
    log:
        "logs/create_trees/RAxML_bestTree.{gene}.log",
    benchmark:
        "benchmarks/create_trees/{gene}.txt"
    conda:
        "envs/raxml_env.yaml"
    shell:
        "cd trees/ && "
        "raxmlHPC -s ../{input} -m GTRCAT -n {wildcards.gene} -p 392781 && "
        "cd .."


rule benchmark_trees:
    input:
        expand("trees/RAxML_bestTree.{gene}", gene=config["GENES"]),
    output:
        "gene_tree_benchmark_report.txt",
    log:
        "logs/benchmark_report.log",
    conda:
        "envs/unifrac_env.yaml"
    script:
        "benchmark_trees.py"


rule merge_trees:
    input:
        expand("trees/RAxML_bestTree.{gene}", gene=config["GENES"]),
    output:
        "trees/final.unrooted",
    log:
        "logs/merge_trees/final.rooted.log",
    benchmark:
        "benchmarks/merge_trees/benchmark.txt"
    conda:
        "envs/openjdk_env.yaml"
    shell:
        "cat {input} > trees/merged.in && "
        "java -jar Astral/astral.5.7.8.jar -i trees/merged.in -o trees/final.unrooted 2>logs/merge_trees/final.unrooted.out.log"


rule infer_weights:
    input:
        "trees/final.unrooted",
    output:
        "trees/iqtree.treefile",
    log:
        "logs/infer_weights/iqtree.treefile.log",
    benchmark:
        "benchmarks/infer_weights/benchmark.txt"
    conda:
        "envs/iqtree_env.yaml"
    params:
        ref=config["GENES"][0],
    shell:
        "iqtree -s filtered-sequences/{params.ref}.fasta -pre trees/iqtree -m MFP -g trees/final.unrooted"


rule outgroup_root_tree:
    input:
        "trees/iqtree.treefile",
    output:
        "RAxML_outgroupRootedTree.final",
    log:
        "logs/root_tree/RAxML_rootedTree.final.log",
    benchmark:
        "benchmarks/outgroup_root_tree/benchmark.txt"
    params:
        ref=config["GENES"][0],
    conda:
        "envs/raxml_env.yaml"
    shell:
        "cd trees/ && "
        "raxmlHPC -s ../filtered-sequences/{params.ref}.fasta -o 2173 -m GTRCAT -t iqtree.treefile -n final && "
        "mv RAxML_result.final ../{output} && "
        "cd .. && "
        "rm -r trees/"


rule midpoint_root_tree:
    input:
        "trees/iqtree.treefile",
    output:
        "RAxML_midpointRootedTree.final",
    log:
        "logs/root_tree/RAxML_rootedTree.final.log",
    benchmark:
        "benchmarks/midpoint_root_tree/benchmark.txt"
    params:
        ref=config["GENES"][0],
    conda:
        "envs/raxml_env.yaml"
    shell:
        "cd trees/ && "
        "raxmlHPC -f I -m GTRCAT -t iqtree.treefile -n final && "
        "mv RAxML_result.final ../{output} && "
        "cd .. && "
        "rm -r trees/"
