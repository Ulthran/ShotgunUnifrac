configfile: "config.yml"


container: "docker://ctbushman/shotguntree:latest"


TARGET = (
    "RAxML_outgroupRootedTree.final"
    if config["OUTGROUP"]
    else "RAxML_midpointRootedTree.final"
)


rule all:
    input:
        TARGET,


rule all_gene_files:
    input:
        expand("sequences/{gene}.fasta", gene=config["GENES"]),


rule all_filtered_files:
    input:
        expand("aligned-sequences/{gene}.fasta", gene=config["GENES"]),


rule align_fasta:
    input:
        "merged-sequences/{gene}.fasta",
    output:
        "aligned-sequences/{gene}.fasta",
    log:
        "logs/align_fasta/{gene}.log",
    conda:
        "envs/muscle_env.yaml"
    shell:
        # Consider adding filtering here as well
        "muscle -in merged-sequences/{wildcards.gene}.fasta -out aligned-sequences/{wildcards.gene}.fasta"


rule create_trees:
    input:
        "aligned-sequences/{gene}.fasta",
    output:
        "trees/RAxML_bestTree.{gene}",
    log:
        "logs/prot_create_trees/RAxML_bestTree.{gene}.log",
    conda:
        "envs/raxml_env.yaml"
    params:
        alg='PROTCATLG' if config["TYPE"] == 'prot' else 'GTRCAT',
    shell:
        "cd trees/ && "
        "raxmlHPC -s ../{input} -m {params.alg} -n {wildcards.gene} -p 392781 && "
        "cd .."


rule merge_trees:
    input:
        expand("trees/RAxML_bestTree.{gene}", gene=config["GENES"]),
    output:
        "trees/final.unrooted",
    log:
        "logs/merge_trees/final.rooted.log",
    conda:
        "envs/openjdk_env.yaml"
    shell:
        "cat {input} > trees/merged.in && "
        "java -jar Astral/astral.5.7.8.jar -i trees/merged.in -o trees/final.unrooted 2>logs/merge_trees/final.unrooted.out.log"


rule infer_weights:
    input:
        "trees/final.unrooted",
    output:
        "trees/iqtree.treefile",
    log:
        "logs/infer_weights/iqtree.treefile.log",
    conda:
        "envs/iqtree_env.yaml"
    params:
        ref=config["GENES"][0],
    shell:
        "iqtree -s aligned-sequences/{params.ref}.fasta -pre trees/iqtree -m MFP -g trees/final.unrooted"


rule outgroup_root_tree:
    input:
        "trees/iqtree.treefile",
    output:
        "RAxML_outgroupRootedTree.final",
    log:
        "logs/root_tree/RAxML_rootedTree.final.log",
    params:
        ref=config["GENES"][0],
        alg='PROTCATLG' if config["TYPE"] == 'prot' else 'GTRCAT',
    conda:
        "envs/raxml_env.yaml"
    shell:
        "cd trees/ && "
        "raxmlHPC -s ../aligned-sequences/{params.ref}.fasta -o GCF_000016525.1.fna -m {params.alg} -t iqtree.treefile -n final && "
        "mv RAxML_result.final ../{output}"


rule midpoint_root_tree:
    input:
        "trees/iqtree.treefile",
    output:
        "RAxML_midpointRootedTree.final",
    log:
        "logs/root_tree/RAxML_rootedTree.final.log",
    params:
        ref=config["GENES"][0],
        alg='PROTCATLG' if config["TYPE"] == 'prot' else 'GTRCAT',
    conda:
        "envs/raxml_env.yaml"
    shell:
        "cd trees/ && "
        "raxmlHPC -f I -m {params.alg} -t iqtree.treefile -n final && "
        "mv RAxML_result.final ../{output}"
